apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = 1.8

evaluationDependsOn(':domain')

mainClassName = 'nobs.Application'

repositories {
    mavenCentral()
}

project.ext {
    jettyVersion = '9.2.1.v20140609'
    javaxWsRsVersion = '2.0'
    jerseyVersion = '2.9'
}

dependencies {
    compile project(':domain')

    compile group: 'org.eclipse.jetty', name: 'jetty-server', version: project.jettyVersion
    compile group: 'org.eclipse.jetty', name: 'jetty-webapp', version: project.jettyVersion
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: project.jettyVersion

    compile group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: project.javaxWsRsVersion

    compile group: 'org.glassfish.jersey.core', name: 'jersey-common', version: project.jerseyVersion
    compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: project.jerseyVersion
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet-core', version: project.jerseyVersion
    compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: project.jerseyVersion
    compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: project.jerseyVersion
}

task frontendBuild(type:Exec) {
    commandLine './make.js'
}

jar {
    manifest {
        attributes("Main-Class": mainClassName)
    }
    from files("src/main/webapp")
    from files("build/frontend-build")
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}
jar.dependsOn frontendBuild

run {
    systemProperty "dev", "true"
    systemProperty "port", "8080"
}